#include <AFMotor.h>

// Motor connections using Adafruit Motor Shield v1
AF_DCMotor motor1(2);
AF_DCMotor motor2(1);

// Sensor configuration
#define NUM_SENSORS 7
const int C = A0;  
const int R3 = A1;  
const int L1 = A2;  
const int R2 = A3;   
const int L3 = A4;  
const int L2 = A5;  
const int R1 = 2;

const int sensorPins[NUM_SENSORS] = {L3, L2, L1, C, R1, R2, R3};
int sensorValues[NUM_SENSORS];

// PID constants
float Kp = 1.0;   // Proportional
float Ki = 0.0;   // Integral
float Kd = 2.0;   // Derivative

// PID variables
int error, lastError = 0;
int P, I, D, PIDvalue;
int baseSpeed = 150;
float speedFactor = 0.2; // Adjust this from 0.0 (stopped) to 1.0 (full speed)

void setup() {
    Serial.begin(115200);
    
    // Initialize motors
    motor1.setSpeed(0);
    motor1.run(RELEASE);
    motor2.setSpeed(0);
    motor2.run(RELEASE);
    
    // Initialize sensors
    for (int i = 0; i < NUM_SENSORS; i++) {
        pinMode(sensorPins[i], INPUT);
    }
}

void loop() {
    int position = readSensors();
    error = (NUM_SENSORS - 1) * 500 - position;

    // PID calculations
    P = error;
    I += error;
    D = error - lastError;
    lastError = error;
    PIDvalue = (Kp * P) + (Ki * I) + (Kd * D);

    int leftSpeed = (baseSpeed - PIDvalue) * speedFactor;
    int rightSpeed = (baseSpeed + PIDvalue) * speedFactor;
    leftSpeed = constrain(leftSpeed, 0, 255);
    rightSpeed = constrain(rightSpeed, 0, 255);

    motor1.setSpeed(leftSpeed);
    motor2.setSpeed(rightSpeed);
    motor1.run(FORWARD);
    motor2.run(FORWARD);
}

int readSensors() {
    int weightedSum = 0;
    int sum = 0;
    for (int i = 0; i < NUM_SENSORS; i++) {
        sensorValues[i] = digitalRead(sensorPins[i]);
        if (sensorValues[i] == 0) {  // Active low sensors
            weightedSum += i * 1000;
            sum++;
        }
    }
    return (sum == 0) ? ((NUM_SENSORS - 1) * 500) : (weightedSum / sum);
}
